<o:MethodTargetItem Id="B3C83083-F981-4187-BC97-B9ABB057A46C">
<a:ObjectID>B3C83083-F981-4187-BC97-B9ABB057A46C</a:ObjectID>
<a:Name>mdde_UninitializeExportExtension</a:Name>
<a:TargetCategory.Type>1</a:TargetCategory.Type>
<a:MethodScript>Sub %Method%(objModel)
   WriteDebug &quot;MDDE LDM Export extension un-initializion started&quot;
   
   &#39; Clear the export attribute on the model.
   objModel.setExtendedAttribute &quot;mddex_Model_Export_Template&quot;, Empty
   objModel.setExtendedAttribute cMDDE_NAMEDOBJECT_ATTRIBUTE_EXPORT_ATTRIBUTE_NAME, Empty
   
   &#39; Clear export attributes in packages recursively, where the model is the root package
   clearExportAttributesInPackage objModel, objModel
   
   &#39; Set initialization indicator to False
   objModel.SetExtendedAttribute cMDDE_EXPORT_EXTENSION_INITIALIZATION_INDICATOR, False
   
   WriteDebug &quot;MDDE LDM Export extension un-initializion finished&quot;
End Sub

Sub clearExportAttributesInPackage(objModel, objPackage)
   &#39;Loop over Entities   
   clearExportAttributesOnCollection objModel, objPackage.Entities, &quot;mddex_Entity_Export_Content&quot;, True
   
   &#39;Loop over Entity Attributes
   clearExportAttributesOnCollection objModel, objPackage.EntityAttributes, &quot;mddex_EntityAttribute_Export_Content&quot;, True
      
   &#39;Loop over Mappings
   clearExportAttributesOnCollection objModel, objPackage.Mappings, &quot;mddex_BaseClassifierMapping_Export_Content&quot;, True
      
   &#39;Loop over AttributeMappings
   clearExportAttributesOnCollection objModel, objPackage.ModelEntityAttributeMappings, &quot;mddex_BaseStructuralFeatureMapping_Export_Content&quot;, False
   
   &#39;Loop over SourceObjects within Mappings
   clearExportAttributesOnExtCollection objModel, objPackage.Mappings, &quot;mdde_SourceObjects&quot;, &quot;mddex_ExtendedSubObject_SourceObject_Export_Content&quot;, True   
   
   &#39; Loop over packages and clear them recursively.
   Dim childPackage
   For Each childPackage in objPackage.Packages
      clearExportAttributesInPackage objModel, childPackage
   Next
End Sub

Sub clearExportAttributesOnCollection(objModel, objCollection, extendedAttributeName, applyNamedObjectTemplate)
   &#39; Loop over collection and set the extended attribute based on the export template
   Dim obj
   For Each obj in objCollection
      &#39; Skip shortcuts.
      If Not obj.IsShortCut Then
         obj.SetExtendedAttribute extendedAttributeName, Empty
         &#39; If specified, also clear the namedobject variable.
         If applyNamedObjectTemplate Then
            obj.SetExtendedAttribute cMDDE_NAMEDOBJECT_ATTRIBUTE_EXPORT_ATTRIBUTE_NAME, Empty
         End If
      End If   
   Next
End Sub

Sub clearExportAttributesOnExtCollection(objModel, colWithObjectsWithExtCol, strExtColName, extendedAttributeName, applyNamedObjectTemplate)
   Dim objCurrentObjectWithExtCol, colExtCol
   &#39; Loop through the objects which have the extended collection.
   For Each objCurrentObjectWithExtCol In colWithObjectsWithExtCol
      &#39; Get the extended collection.
      Set colExtCol = objCurrentObjectWithExtCol.GetExtendedCollection(strExtColName)
      &#39; Only proceed if there is an extended collection with the given name.
      If Not(colExtCol Is Nothing) Then
         &#39; Clear the extended attribute for each items in the extended collection.
         clearExportAttributesOnCollection objModel, colExtCol, extendedAttributeName, applyNamedObjectTemplate
      End If
   Next
End Sub</a:MethodScript>
</o:MethodTargetItem>